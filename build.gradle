apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven-publish'

compileJava {
  options.compilerArgs << '-Werror'
  options.compilerArgs << '-Xlint:all'
  // Don't fail for annotations not claimed by annotation processors.
  options.compilerArgs << '-Xlint:-processing'
  // Don't fail for serialVersionUID warnings.
  options.compilerArgs << '-Xlint:-serial'
  sourceCompatibility = 1.7
  targetCompatibility = 1.7
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId 'com.twitter.aurora'
      artifactId 'twitter-aurora'
      version '0.0.2'

      from components.java

      artifact sourceJar {
        classifier "sources"
      }
    }
  }
  repositories {
    maven {
      credentials {
        username = internalMavenUser
        password = internalMavenPass
      }
      url internalMavenUrl
    }
  }
}

repositories {
  mavenCentral()
  maven {
    url 'http://maven.twttr.com/'
  }
  // TODO(William Farner): Remove this when source is fully detached from internal.
  maven {
    url internalMavenUrl
  }
}

dependencies {
  compile 'aopalliance:aopalliance:1.0'
  compile 'com.google.code.findbugs:jsr305:1.3.9'
  compile 'com.google.guava:guava:14.0.1'
  compile 'com.google.inject:guice:3.0'
  compile 'com.google.protobuf:protobuf-java:2.4.1'
  def jerseyRev = '1.12'
  compile "com.sun.jersey:jersey-core:${jerseyRev}"
  compile "com.sun.jersey:jersey-json:${jerseyRev}"
  compile "com.sun.jersey:jersey-server:${jerseyRev}"
  compile "com.sun.jersey:jersey-servlet:${jerseyRev}"
  compile "com.sun.jersey.contribs:jersey-guice:${jerseyRev}"
  compile 'commons-lang:commons-lang:2.5'
  compile 'javax.servlet:servlet-api:2.5'
  compile 'log4j:log4j:1.2.17'
  compile 'org.antlr:stringtemplate:3.2.1'
  compile 'org.apache.mesos:mesos:0.13.0'
  compile 'org.apache.thrift:libthrift:0.5.0-1'
  compile 'org.apache.zookeeper:zookeeper:3.3.4'
  def slf4jRev = '1.6.1'
  compile "org.slf4j:slf4j-api:${slf4jRev}"
  compile "org.slf4j:slf4j-jdk14:${slf4jRev}"
  compile 'com.twitter.aurora:aurora:0.0.2'
  compile 'com.twitter.common.logging:log4j:0.0.3'
  compile 'com.twitter.common.webassets:bootstrap:0.0.1'
  compile 'com.twitter.common.webassets:jquery:0.0.1'
  compile 'com.twitter.common.zookeeper.guice:client-flagged:0.0.2'
  compile 'com.twitter.common.zookeeper.guice:client:0.0.2'
  compile 'com.twitter.common.zookeeper:candidate:0.0.48'
  compile 'com.twitter.common.zookeeper:client:0.0.40'
  compile 'com.twitter.common.zookeeper:group:0.0.51'
  compile 'com.twitter.common.zookeeper:server-set:1.0.52'
  compile 'com.twitter.common.zookeeper:singleton-service:0.0.65'
  compile 'com.twitter.common:application-http:0.0.49'
  compile 'com.twitter.common:application-module-applauncher:0.0.43'
  compile 'com.twitter.common:application-module-http:0.0.46'
  compile 'com.twitter.common:application-module-lifecycle:0.0.39'
  compile 'com.twitter.common:application-module-log:0.0.47'
  compile 'com.twitter.common:application-module-stats:0.0.39'
  compile 'com.twitter.common:application:0.0.65'
  compile 'com.twitter.common:args:0.1.71'
  compile 'com.twitter.common:base:0.0.72'
  compile 'com.twitter.common:collections:0.0.59'
  compile 'com.twitter.common:dynamic-host-set:0.0.35'
  compile 'com.twitter.common:inject-timed:0.0.8'
  compile 'com.twitter.common:inject:0.0.29'
  compile 'com.twitter.common:io:0.0.42'
  compile 'com.twitter.common:jdk-logging:0.0.34'
  compile 'com.twitter.common:logging:0.0.49'
  compile 'com.twitter.common:net-http-filters:0.0.3'
  compile 'com.twitter.common:net-util:0.0.65'
  compile 'com.twitter.common:quantity:0.0.56'
  compile 'com.twitter.common:stats:0.0.81'
  compile 'com.twitter.common:testing-easymock:0.0.1'
  compile 'com.twitter.common:thrift:0.0.61'
  compile 'com.twitter.common:util-executor-service-shutdown:0.0.38'
  compile 'com.twitter.common:util-system-mocks:0.0.55'
  compile 'com.twitter.common:util-templating:0.0.17'
  compile 'com.twitter.common:util-testing:0.0.6'
  compile 'com.twitter.common:util:0.0.83'
  compile 'com.twitter.common:zookeeper-testing:0.0.37'
  testCompile 'junit:junit:4.10'
}

sourceSets {
  test {
    resources {
      srcDir 'src/main/resources'
      srcDir 'src/test/resources'
    }
  }
}

// Configuration parameters for the application plugin.
applicationName = 'aurora-scheduler'
mainClassName = 'com.twitter.aurora.scheduler.app.SchedulerMain'
